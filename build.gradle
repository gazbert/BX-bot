buildscript {

    repositories {
        jcenter()
        mavenLocal()
    }

    dependencies {
        classpath("io.spring.gradle:dependency-management-plugin:1.0.5.RELEASE")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:2.0.2.RELEASE")
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.13.1'
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

ext.versions = [
        springBootVersion:'2.0.2.RELEASE',
        springTxVersion:'5.0.6.RELEASE'
]

ext.libraries = [
        spring_boot_starter: dependencies.create("org.springframework.boot:spring-boot-starter:" + ext.versions.springBootVersion) {
            exclude module: "spring-boot-starter-logging"
            force = true
        },
        spring_boot_starter_log4j2: dependencies.create("org.springframework.boot:spring-boot-starter-log4j2:" + ext.versions.springBootVersion),
        spring_boot_starter_web: dependencies.create("org.springframework.boot:spring-boot-starter-web:" + ext.versions.springBootVersion) {
            exclude module: "spring-boot-starter-logging"
            force = true
        },
        spring_boot_starter_actuator: dependencies.create("org.springframework.boot:spring-boot-starter-actuator:" + ext.versions.springBootVersion) {
            exclude module: "spring-boot-starter-logging"
            force = true
        },
        spring_boot_starter_security: dependencies.create("org.springframework.boot:spring-boot-starter-security:" + ext.versions.springBootVersion) {
            exclude module: "spring-boot-starter-logging"
            force = true
        },
        spring_tx: dependencies.create("org.springframework:spring-tx:" + ext.versions.springTxVersion),
        google_guava: dependencies.create("com.google.guava:guava:25.0-jre"),
        google_gson: dependencies.create("com.google.code.gson:gson:2.8.4"),
        javax_mail_api: dependencies.create("javax.mail:javax.mail-api:1.6.1"),
        javax_mail_sun: dependencies.create("com.sun.mail:javax.mail:1.6.1"),

        javax_xml_api: dependencies.create("javax.xml.bind:jaxb-api:2.3.0"),
        javax_xml_impl: dependencies.create("com.sun.xml.bind:jaxb-impl:2.3.0"),
        javax_xml_core: dependencies.create("com.sun.xml.bind:jaxb-core:2.3.0"),
        javax_xml_runtime: dependencies.create("org.glassfish.jaxb:jaxb-runtime:2.3.0"),
        javax_activation: dependencies.create("javax.activation:activation:1.1.1"),

        junit: dependencies.create("junit:junit:4.12"),
        powermock_junit: dependencies.create("org.powermock:powermock-module-junit4:2.0.0-beta.5"),
        powermock_api_easymock: dependencies.create("org.powermock:powermock-api-easymock:2.0.0-beta.5"),
        easymock: dependencies.create("org.easymock:easymock:3.6") {
            exclude module: "cglib-nodep"
            force = true
        },
        objenesis: dependencies.create("org.objenesis:objenesis:2.6"),
        cglib_nodep: dependencies.create("cglib:cglib-nodep:3.2.6"),
        spring_boot_starter_test: dependencies.create("org.springframework.boot:spring-boot-starter-test:" + ext.versions.springBootVersion) {
            exclude module: "spring-boot-starter-logging"
            force = true
        }
]

allprojects {

    apply plugin: "io.spring.dependency-management"
    apply plugin: 'maven'

    group = 'com.gazbert.bxbot'
        version = '0.10.0-SNAPSHOT'
}

subprojects {

    apply plugin: 'java'

    sourceCompatibility = 1.10
    targetCompatibility = 1.10

    repositories {
        mavenLocal()
        maven { url "http://repo.maven.apache.org/maven2" }
    }

    test {
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    javadoc {
        include "**/api/**"
    }
}

// TODO strip out common stuff into separate method?
task buildZipDist(type: Zip, dependsOn: build) {

    baseName = 'bxbot-app'

    into("$baseName-$version") {

        into('libs') {
            from 'bxbot-app/build/libs'
            include '*.jar' //copy the fat jar created by bootRepackage
        }

        into('') {
            from 'README.md'
        }

        into('') {
            from 'CONTRIBUTING.md'
        }

        into('') {
            from 'LICENSE'
        }

        into('') {
            from 'third_party_licenses.txt'
        }

        into('') {
            from 'MAINTAINERS.txt'
        }

        into('') {
            from 'bxbot.sh'
        }

        into('') {
            from 'bxbot.bat'
        }

        from('config/application.properties') {
            into 'config'
        }

        into('config') {
            from 'config'
            include('*.xml')
        }
    }
}

// TODO strip out common stuff into separate method?
task buildTarGzipDist(type: Tar, dependsOn: build) {

    baseName = 'bxbot-app'
    extension = 'tar.gz'
    compression = Compression.GZIP

    into("$baseName-$version") {

        into('libs') {
            from 'bxbot-app/build/libs'
            include '*.jar' //copy the fat jar created by bootRepackage
        }

        into('') {
            from 'README.md'
        }

        into('') {
            from 'CONTRIBUTING.md'
        }

        into('') {
            from 'LICENSE'
        }

        into('') {
            from 'third_party_licenses.txt'
        }

        into('') {
            from 'MAINTAINERS.txt'
        }

        into('') {
            from 'bxbot.sh'
        }

        into('') {
            from 'bxbot.bat'
        }

        from('config/application.properties') {
            into 'config'
        }

        into('config') {
            from 'config'
            include('*.xml')
        }
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '4.7'
    }
}